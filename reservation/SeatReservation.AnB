Protocol: SeatReservation

Types: Agent Customer,s,Venue;
       Number RequestSeat,ReqID,Confirmation;
       Symmetric_key K,KCs;
       Function pk,pw;

Knowledge: 
       Customer: Customer,s,pw(Customer,s),pk(s),Venue,pk(Venue);
       Venue: Venue,pk(Venue),inv(pk(Venue)),s,pk(s);
       s: s,pk(s),inv(pk(s)),Venue,pk(Venue),Customer,pw(Customer,s);
       where Venue!=s

Actions:

Customer->Venue: {Customer,Venue,RequestSeat,K}pk(Venue) 
Venue->Customer: {|Customer,s,Venue,ReqID|}K

Customer->s:  { {| {| Customer,s,Venue,ReqID |}pw(Customer,s) |}KCs, KCs }pk(s)
s->Venue:  { {Customer,s,Venue,ReqID }inv(pk(s)) }pk(Venue)

Venue->Customer: {|RequestSeat,Confirmation|}K

Goals:

Venue authenticates Customer on RequestSeat
Customer authenticates Venue on Confirmation
Confirmation secret between Venue,Customer
pw(Customer,s) guessable secret between Customer,s
