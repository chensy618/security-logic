Protocol: SeatReservation

Types: Agent Customer,s,Venue;
       Number RequestSeat,ReqID,Confirmation;
       Symmetric_key KCV,KCs;
       Function pk,pw;

Knowledge: 
       Customer: Customer,s,pw(Customer,s),pk(s),Venue,pk(Venue);
       Venue: Venue,pk(Venue),inv(pk(Venue)),s,pk(s);
       s: s,pk(s),inv(pk(s)),Venue,pk(Venue),Customer,pw(Customer,s);
       where Venue!=s

Actions:

Customer->Venue: {Customer,Venue,RequestSeat,KCV}pk(Venue) 
Venue->Customer: {|Customer,s,Venue,ReqID|}KCV

Customer->s:  { {| {| Customer,s,Venue,ReqID |}pw(Customer,s) |}KCs, KCs }pk(s)
s->Venue:  { {Customer,s,Venue,ReqID }inv(pk(s)) }pk(Venue)

Venue->Customer: {|RequestSeat,Confirmation|}KCV

Goals:

Venue authenticates Customer on RequestSeat
Customer authenticates Venue on Confirmation
Confirmation secret between Venue,Customer
KCs secret between Customer,s
pw(Customer,s) guessable secret between Customer,s
